// Copyright © 2019, SAS Institute Inc., Cary, NC, USA.  All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

Names Default To Here(0);

ut addin home = If(Is Empty(ut addin home), Convert File Path("$ADDIN_HOME(com.jmp.jslhamcrest)"), ut addin home);
ut addin scratch = If(Is Empty(ut addin scratch), ut addin home, ut addin scratch);

ut meets hamcrest requirements = Function({}, {version, major, minor},
	version = Words(JMP Version(), ".");
	major = Num(version[1]);
	minor = Num(version[2]);
	If(major < 14 | (major == 14 & minor < 1), 0, 1)
);

If(!ut meets hamcrest requirements(),
	New Window("Error", <<Modal, Text Box("JSL Hamcrest requires at least JMP 14.1"));
	Stop();
);

// If reporter does not exist or is the wrong type, reset the framework. If we have
// configured reporter (not UtReporter), then use it as the fallback for the
// UtWindowDispatchingReporter. Otherwise, use logging for the fallback.
Local({reporter name, fallback reporter},
	reporter name = Try(::ut global reporter << Get Name, "UtReporter");
	If(reporter name != "UtWindowDispatchingReporter",
		Include( "../../All.jsl" );
		Include( "../Reporters.jsl" );
		Include( "../TestRunner.jsl" );
		Include( "../TestRunnerPreferences.jsl" );
		fallback reporter = If(reporter name != "UtReporter", ::ut global reporter, ut streaming log reporter with skip());
		::ut global reporter = ut window dispatching reporter(fallback reporter);
	);
);
